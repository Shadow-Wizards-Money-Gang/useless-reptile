plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

version = project.mod_version + "-" + project.minecraft_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url 'https://maven.isxander.dev/releases' }
    maven { url "https://maven.nucleoid.xyz/" }
    maven {url 'https://libs.azuredoom.com:4443/mods'}
    maven { url 'https://jitpack.io' }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:yacl:2.2.0-for-1.19.2"
    modApi "com.terraformersmc:modmenu:${project.modmenu_version}"
    modImplementation include("eu.pb4:common-protection-api:${project.protection_api_version}")
    modImplementation include ("com.github.NordAct:Primitive-Multipart-Entities:${project.primitive_multipart_entities_version}")
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.5")))
    modImplementation "mod.azure.azurelib:azurelib-fabric-1.19.2:1.0.41"
    //modImplementation("maven.modrinth:carpet:1.4.128")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    withSourcesJar()
}

configurations.all {
    resolutionStrategy {
        force("net.fabricmc:fabric-loader:$loader_version")
    }
}

loom {
    accessWidenerPath = file("src/main/resources/uselessreptile.accesswidener")
    runs {
        server {
            server()
            vmArg "-Dgeckolib.disable_examples=true"
        }
        client {
            client()
            vmArg "-Dgeckolib.disable_examples=true"
            programArgs "--username", "Nord_Act"
        }
    }
}

publishMods {
    file = remapJar.archiveFile
    changelog = project.file("CHANGELOG.md").getText()
    type = BETA
    displayName = "Useless Reptile ${project.version}"
    modLoaders.add("fabric")
    //dryRun = true

    curseforge {
        projectId = "677634"
        projectSlug = "useless-reptile"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(project.property("minecraft_version") as String)
        requires("fabric-api", "yacl", "azurelib")
    }
    modrinth {
        projectId = "YE4QRt8F"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(project.property("minecraft_version") as String)
        requires("fabric-api", "yacl", "azurelib")
    }
}